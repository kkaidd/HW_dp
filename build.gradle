plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

allure {
    report {
        version.set("2.27.0")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.27.0")
            }
        }
    }
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'com.codeborne:selenide:6.19.1'
    testImplementation 'io.qameta.allure:allure-selenide:2.27.0'
    testImplementation 'org.slf4j:slf4j-simple:2.0.12'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

// Задача для тестов с тегом High
task high_tests(type: Test) {
    useJUnitPlatform {
        includeTags("High")
    }
    description = 'Runs tests with High priority tag'
    group = 'verification'
}

// Задача для тестов с тегом Medium
task medium_tests(type: Test) {
    useJUnitPlatform {
        includeTags("Medium")
    }
    description = 'Runs tests with Medium priority tag'
    group = 'verification'
}
